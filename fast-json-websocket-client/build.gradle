plugins {
	id 'java'
	id 'findbugs'
	id "com.jfrog.bintray" version "1.8.0"
	id 'maven'
	id 'maven-publish'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

def pluginGroup = "me.joshlarson"
def pluginName = "fast-json-websocket-client"
def pluginVersion = "1.4.1"

group = pluginGroup
version = pluginVersion

repositories {
    jcenter()
}

dependencies {
	compile group: 'me.joshlarson', name: 'fast-json', version: '2.2.3'
	compile 'com.neovisionaries:nv-websocket-client:2.3'
	
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

jar {
	baseName = "fast-json-websocket-client"
	classifier = null
	version = pluginVersion
}

task sourcesJar(type: Jar, dependsOn: classes) {
	baseName = "fast-json-websocket-client"
	classifier = "sources"
	version = pluginVersion
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	baseName = "fast-json-websocket-client"
	classifier = "javadoc"
	version = pluginVersion
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

task wrapper(type: Wrapper) {
	gradleVersion = "4.4"
}

def pomConfig = {
	licenses {
		license {
			name "The MIT License"
			url "https://opensource.org/licenses/MIT"
			distribution "repo"
		}
	}
	developers {
		developer {
			id "josh-larson"
			name "Josh Larson"
			email "joshua-github@ourclan.net"
		}
	}
	
	scm {
		url "https://github.com/Josh-Larson/fast-json-websocket"
	}
}

// Create the publication with the pom configuration:
publishing {
	publications {
		FastJsonWebsocketPublication(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			groupId pluginGroup
			artifactId pluginName
			version pluginVersion
			pom.withXml {
				def root = asNode()
				root.appendNode('description', 'A JSON web socket library written in Java')
				root.appendNode('name', 'Fast JSON Websocket')
				root.appendNode('url', 'https://github.com/Josh-Larson/fast-json-websocket')
				root.children().last() + pomConfig
			}
		}
	}
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
	publications = ['FastJsonWebsocketPublication']
	pkg {
		repo = 'fast-json-websocket'
		name = pluginName
		licenses = ['MIT']
		vcsUrl = 'https://github.com/Josh-Larson/fast-json-websocket.git'
		version {
			name = pluginVersion
			desc = 'Fast JSON Websocket'
			released = new Date()
			vcsTag = 'v' + pluginVersion
		}
	}
}
